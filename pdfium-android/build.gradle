plugins {
    alias(libs.plugins.android.library)
    id("maven-publish")
}

android {
    namespace 'com.bellalhossainmondal.pdfium'
    compileSdk 35

    defaultConfig {
        minSdk 21

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    ndkVersion "27.0.12077973"
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.22.1"
        }
    }

    sourceSets {
        main {
            jni.srcDirs = []
            jniLibs.srcDirs = ['src/main/jniLibs']
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = "com.github.bellalhossainmondal"
            artifactId = "pdfium-android"
            version = '1.0.0'

            afterEvaluate {
                from components.release
            }

            pom {
                name.set("pdfium-android")
                description.set("Fork of library for rendering PDFs on Android's Surface or Bitmap")
                url.set("https://github.com/bellalhossainmondal/pdfium-android")
                licenses {
                    license {
                        name.set("The Apache Software License, Version 2.0")
                        url.set("http://www.apache.org/licenses/LICENSE-2.0.txt")
                    }
                }
                developers {
                    developer {
                        id.set("bellalhossainmondal")
                        name.set("Bellal Hossain Mondal")
                        email.set("bellalmondalofficial@gmail.com")
                    }
                }
                scm {
                    connection.set("scm:git:github.com/bellalhossainmondal/pdfium-android.git")
                    developerConnection.set("scm:git:ssh://github.com/bellalhossainmondal/pdfium-android.git")
                    url.set("https://github.com/bellalhossainmondal/pdfium-android")
                }
            }
        }
    }
}

dependencies {

    implementation libs.appcompat
    implementation libs.material
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}